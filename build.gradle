plugins {
    id 'java-library'
	id 'com.github.ben-manes.versions' version '0.44.0'
    id 'maven-publish'
    id "io.github.krakowski.jextract" version "0.3.1"
}

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

group = 'io.calimero'
version = '2.6-SNAPSHOT'

ext.junitJupiterVersion = '5.9.1'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(19))
    }
    withSourcesJar()
    withJavadocJar()
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
javadoc.options.encoding = 'UTF-8'

compileJava.options*.compilerArgs = [
	'-Xlint:all,-serial',
	"--limit-modules", "java.base",
	"--enable-preview"
]

compileTestJava.options*.compilerArgs = [
	'-Xlint:all',
	'-Xlint:-try',
	"--enable-preview"
]

tasks.withType(Javadoc) {
    options.addStringOption("Xdoclint:-missing", '-quiet')
    options.addBooleanOption('-enable-preview', true)
	options.addStringOption("-release", "${targetCompatibility}")
}

tasks.withType(Jar) {
	from("${projectDir}") {
        include 'LICENSE'
        into "META-INF"
    }
    if (name.equals('sourcesJar')) {
    	duplicatesStrategy = 'EXCLUDE'
    	from("${projectDir}") {
		    include 'README.md', 'build.gradle', 'settings.gradle', 'gradle*/**'
        }
    }
}

test {
	useJUnitPlatform() {
		testLogging {
			exceptionFormat "full"
//			showStandardStreams true
		}
	}
	jvmArgs(['--enable-preview'])
}

tasks.withType(JavaExec) { jvmArgs += "--enable-preview" }


//project.gradle.startParameter.excludedTaskNames.add("jextract")

jextract {
	def os = System.getProperty("os.name").toLowerCase(Locale.ENGLISH)

	def headersDir = "unix"
	def path = ""
	def tgtPkg = "org.unix"
	def clsName = "Linux"

	if (os.contains("windows")) {
		headersDir = "win"
		path = "C:/mingw64/x86_64-w64-mingw32/include"
		tgtPkg = 'org.win'
		clsName = 'Windows'
	}
	else if (os.contains("linux")) { 
		path = "/usr/include"
	}
	else if (os.contains("mac")) {
		path = "/Applications/Xcode.app/Contents/Developer/Platforms/MacOSX.platform/Developer/SDKs/MacOSX.sdk/usr/include"
	}
	else {
		throw new RuntimeException("Unsupported platform '" + os + "'")
	}

	header("${project.projectDir}/src/include/$headersDir/headers.h") {
		targetPackage = tgtPkg
		className = clsName
		includes = [ "$path" ]
	}
}

dependencies {
    api "com.github.calimero:calimero-core:${version}"
    testImplementation "org.junit.jupiter:junit-jupiter:${junitJupiterVersion}"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = rootProject.name
            from components.java
            pom {
                name = 'Calimero serial-native service provider'
                description = 'Serial comummication provider using FFM'
                url = 'https://github.com/calimero-project/calimero-serialcom'
                inceptionYear = '2006'
                licenses {
                    license {
                        name = "GNU General Public License, version 2, with the Classpath Exception"
                        url = 'LICENSE'
                    }
                }
                developers {
                    developer {
                        name = "Boris Malinowsky"
                        email = "b.malinowsky@gmail.com"
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com:calimero-project/calimero-serialcom.git'
                    url = 'https://github.com/calimero-project/calimero-serialcom.git'
                }
            }
        }
    }
    repositories {
        maven {
            name 'maven'
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials(PasswordCredentials)
        }
    }
}
